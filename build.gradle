plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'com.danifoldi'
version '2.0.0'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    google()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://mvn.exceptionflug.de/repository/exceptionflug-public/' }
}

dependencies {
    compileOnly 'io.github.waterfallmc:waterfall-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'dev.simplix:protocolize-api:2.0.1'

    compileOnly 'net.luckperms:api:5.3'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.7.3'

    implementation ('com.electronwill.night-config:yaml:3.6.5') {
        exclude module: 'snakeyaml'
    }

    compileOnly 'org.jetbrains:annotations:22.0.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:dagger:2.40'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40.1'
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from(sourceSets.main.resources) {
        expand version: project.version
    }
}

shadowJar {
    minimize {
        exclude(dependency('com.electronwill.night-config:.*:.*'))
    }

    relocate 'org.jetbrains.annotations', 'com.danifoldi.bungeegui.lib.annotations'
    relocate 'javax.inject', 'com.danifoldi.bungeegui.lib.inject'
    relocate 'dagger', 'com.danifoldi.bungeegui.lib.dagger'
    relocate 'com.electronwill.night-config', 'com.danifoldi.bungeegui.lib.nightconfig'

    archiveFileName.set("${project.name}-${project.version}.jar")
}