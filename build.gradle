plugins {
    id 'java'
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.danifoldi'
version '2.4.1'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    google()
    maven { url 'https://repo.danifoldi.com/repository/maven-releases/' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://mvn.exceptionflug.de/repository/exceptionflug-public/' }
    maven { url 'https://repo.simplix.dev/repository/simplixsoft-public/' }
}

dependencies {
    compileOnly 'io.github.waterfallmc:waterfall-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'

    implementation ('grapefruit:grapefruit:1.4.0') {
        exclude module: 'annotation'
        exclude module: 'guava'
    }
    compileOnly 'dev.simplix:protocolize-api:2.1.1'

    compileOnly 'net.luckperms:api:5.3'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.7.3'
    compileOnly 'dev.simplix.plugins:punishcontrol-api:2.0.1'

    implementation ('com.electronwill.night-config:yaml:3.6.5') {
        exclude module: 'snakeyaml'
    }

    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:dagger:2.40.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40.5'
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from(sourceSets.main.resources) {
        expand version: project.version
    }
}

blossom {
    replaceTokenIn('src/main/java/com/danifoldi/protogui/platform/velocity/ProtoGui.java')
    replaceToken '@version@', project.version
}

shadowJar {
    minimize {
        //noinspection GroovyAssignabilityCheck
        exclude(dependency('com.electronwill.night-config:.*:.*'))
    }

    relocate 'org.jetbrains.annotations', 'com.danifoldi.protogui.lib.annotations'
    relocate 'javax.inject', 'com.danifoldi.protogui.lib.inject'
    relocate 'dagger', 'com.danifoldi.protogui.lib.dagger'
    relocate 'com.electronwill.nightconfig', 'com.danifoldi.protogui.lib.nightconfig'
    relocate 'grapefruit.command', 'com.danifoldi.protogui.lib.grapefruit'

    archiveFileName.set("${project.name}-${project.version}.jar")
}